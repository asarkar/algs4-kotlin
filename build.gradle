buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

subprojects {
    group = "org.abhijitsarkar.kalgs4"
    version = "1.0.0-SNAPSHOT"

    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        jcenter()
    }

    dependencies {
        implementation(
                "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        )
        testImplementation(
                "org.jetbrains.kotlin:kotlin-test",
                "org.jetbrains.kotlin:kotlin-test-junit",
                "edu.princeton.cs:algs4:$algsVersion"
        )
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
            allWarningsAsErrors = true
        }
    }

    jar {
        baseName = rootProject.name
        appendix = project.name
    }

    test {
        testLogging.showStandardStreams = logger.isEnabled(LogLevel.INFO)
        failFast = true
    }
}

project(":test") {
    configurations {
        unitTest.extendsFrom testImplementation
    }

    task testJar(type: Jar) {
        from sourceSets.test.output.classesDirs
        classifier = "tests"
    }

    artifacts {
        unitTest testJar
    }
}

project(":sorting") {
    dependencies {
        implementation(
                project(":core")
        )
    }
}

project(":searching") {
    dependencies {
        implementation(
                project(":core"),
                project(":sorting")
        )
    }
}

project(":graph") {
    dependencies {
        implementation(
                project(":core"),
                project(":sorting")
        )
        testImplementation(project(path: ":test", configuration: "unitTest"))
    }
}
